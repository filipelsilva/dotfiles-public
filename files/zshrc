# Variables {{{
if (( $+commands[nvim] )); then
	export EDITOR="nvim"
	export MANPAGER="nvim +Man!"
else
	export EDITOR="vim"
	export MANPAGER="env MAN_PN=1 vim -M +MANPAGER -"
fi
export VISUAL="$EDITOR"

# Adding folders to PATH {{{
PATH=$HOME/.local/bin:$PATH

(( $+commands[cargo] )) && PATH=$HOME/.cargo/bin:$PATH

if (( $+commands[go] )); then
	export GOPATH=$HOME/go
	PATH=$GOPATH/bin:$PATH
fi

if (( $+commands[java] )); then
	export JAVA_HOME=/usr/lib/jvm/default
	PATH=$JAVA_HOME/bin:$PATH
fi
# }}}
export PATH

(( $+commands[python] )) && export PYTHONDONTWRITEBYTECODE=1

if (( $+commands[bat] )); then
	export BAT_THEME="ansi"
	export BAT_STYLE="auto"
fi
# }}}

# Aliases {{{

# Basic commands
alias ..="cd .."
alias -- -="cd -"
alias cp="cp -r"
alias bc="bc -l"
alias mkdir="mkdir -p"
alias wget="wget -c"
alias ip="ip --color=auto"
alias diff="diff --color=auto"
alias grep="grep --color=auto"
alias v="$EDITOR"
if (( $+commands[nvim] )); then
	alias vimdiff="nvim -d"
fi

# Ls aliases
alias lt="tree --dirsfirst -F" # -C"
alias lta="lt -a"
alias ls="ls -hCF --group-directories-first" # --color=auto"
alias lsa="ls -A"
alias l="ls -l"
alias la="ls -lA"
alias lx="ls -lisa"
alias lr="ls -lR"
alias lrs="ls -R"
alias lra="ls -lAR"

# Ssh with xterm, for targets without alacritty
alias ssh="TERM=xterm-256color ssh"

# Search for processes
if (( $+commands[fzf] )); then
	alias psfzf="ps aux | fzf --header='[name:process]'"
fi
alias psgrep="ps aux | grep -v grep | grep -i -e VSZ -e"

# Zsh configuration and reload
alias zshsource="source $HOME/.zshrc && echo 'sourced zshrc'"
alias zshconfig="$EDITOR $HOME/.zshrc && zshsource"

# i3 configuration and reload
if (( $+commands[i3] )); then
	alias i3source="i3-msg restart"
	alias i3config="$EDITOR $HOME/.config/i3/config && i3source"
fi

# GDB aliases
if [ -f $HOME/.gdbinit ]; then
	alias pwndbg="gdb -quiet -ex init-pwndbg"
	alias gef="gdb -quiet -ex init-gef"
fi
# }}}

# Prompt {{{
setopt prompt_subst

autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )

zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:*' unstagedstr '*'
zstyle ':vcs_info:*' formats '%b%m%u%c'

export RPROMPT='${vcs_info_msg_0_}'

# Replace %# with %(!.#.$) for bash-like prompt
export PROMPT="%n@%M:%~%# "
# }}}

# Directory stack {{{
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushd_silent

alias d="dirs -v"
for index ({1..9}); alias "$index"="cd +${index}"; unset index

autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
# }}}

# Completion/Correction {{{
zmodload zsh/complist

# Vi mode for selecting completion
bindkey -M menuselect "h" vi-backward-char
bindkey -M menuselect "k" vi-up-line-or-history
bindkey -M menuselect "j" vi-down-line-or-history
bindkey -M menuselect "l" vi-forward-char

autoload -U compinit && compinit
autoload -U +X bashcompinit && bashcompinit
_comp_options+=(globdots)

setopt always_to_end
setopt auto_cd
setopt auto_list
setopt auto_menu
setopt auto_param_slash
setopt complete_in_word
setopt extended_glob
setopt glob_complete
setopt list_types
unsetopt flow_control

zstyle ':completion:*' add-space true
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' cache-path $HOME/.zcompcache
zstyle ':completion:*' completer _expand _complete _ignored _expand_alias _extensions _match _correct _approximate _prefix
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort name
zstyle ':completion:*' format '-- %d --'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' group-order aliases functions builtins commands
zstyle ':completion:*' ignore-parents parent pwd
zstyle ':completion:*' insert-unambiguous false
zstyle ':completion:*' list-prompt "%SAt %p: Hit TAB for more, or the character to insert%s"
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'r:|[._-]=* r:|=**' 'l:|=* r:|=*'
zstyle ':completion:*' match-original both
zstyle ':completion:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/2))numeric)'
zstyle ':completion:*' menu select=0
zstyle ':completion:*' original true
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' select-prompt "%SScrolling active: current selection at %p%s"
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' use-cache on
zstyle ':completion:*' verbose true
zstyle ':completion:*' word true

zstyle ':completion:*:*:*:*:corrections' format '-- %d (errors: %e) --'
zstyle ':completion:*:*:*:*:descriptions' format '-- %d --'
zstyle ':completion:*:*:*:*:messages' format '-- %d --'
zstyle ':completion:*:*:*:*:warnings' format '-- no matches found --'
# }}}

# Vi-mode and keybinds {{{
autoload -z edit-command-line
zle -N edit-command-line

KEYTIMEOUT=1

bindkey -v
bindkey "^[[Z" reverse-menu-complete
bindkey "^[[3~" delete-char
bindkey "^?" backward-delete-char
bindkey "\e." insert-last-word
bindkey "^E" edit-command-line

# Add text objects to command line
autoload -Uz select-bracketed select-quoted
zle -N select-quoted
zle -N select-bracketed
for km in viopp visual; do
	for c in {a,i}${(s..)^:-\'\"\`\|,./:;=+@}; do
		bindkey -M $km -- $c select-quoted
	done
	for c in {a,i}${(s..)^:-"()[]{}<>bB"}; do
		bindkey -M $km -- $c select-bracketed
	done
done
# }}}

# Command history {{{
HISTFILE=$HOME/.zhistory
HISTSIZE=100000
SAVEHIST=100000

setopt extended_history
setopt hist_expire_dups_first
setopt hist_find_no_dups
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
setopt inc_append_history_time

autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

# Keybinds
bindkey "^[[A" up-line-or-beginning-search
bindkey "^[[B" down-line-or-beginning-search
bindkey "^P" up-line-or-beginning-search
bindkey "^N" down-line-or-beginning-search
bindkey -M vicmd "k" up-line-or-beginning-search
bindkey -M vicmd "j" down-line-or-beginning-search

bindkey "^R" history-incremental-search-backward
bindkey -M vicmd "/" history-incremental-search-backward
# }}}

# Options {{{
setopt hashlistall
setopt longlistjobs
setopt nobeep
setopt noglobdots
setopt nohup
setopt noshwordsplit
setopt notify
# }}}

# Functions {{{

# [Take]: mkdir directory and cd to it
function take() {
	mkdir -p $@ && cd ${@:$#}
}

# [Checksum] files
function checksum() {
	if [ "$#" -ne 2 ]; then
		echo "Usage: checksum <file> <intended result>"
	fi
	echo "$2  $1" | shasum -a 256 -c
}

# [Open] files
function open() {
	if [ "$#" -ne 0 ]; then
		for arg in $@; do
			(xdg-open $arg > /dev/null 2>&1 &)
		done
	else
		(fzf --multi | xargs -I {} sh -c "xdg-open '{}' > /dev/null 2>&1 &")
	fi
}

# [Linkdump]
function linkdump() {
	lynx -dump -listonly -nonumbers $1 | grep .pdf > dump.txt
	wget -i dump.txt
	rm dump.txt
}

# [F]ind-[I]n-[F]ile
function fif() {
	if [ ! "$#" -gt 0 ]; then
		echo "Need a string to search for!";
		return 1;
	fi
	rg --files-with-matches --no-messages "$1" | fzf --header="[find in file]" --preview "rg --pretty --context 10 '$1' {}"
}

# [K]ill [P]rocess
function kp() {
	local pid=$(ps -ef | sed 1d | eval "fzf --header='[kill:process]'" | awk '{print $2}')
	if [ "x$pid" != "x" ]; then
		echo $pid | xargs kill -${1:-9}
		kp
	fi
}

# Start[x]: to use with optimus-manager, instead of default startx
function x() {
	if systemctl -q is-active graphical.target && [[ ! $DISPLAY && $XDG_VTNR -eq 1 ]];
	then
		sudo /usr/bin/prime-switch
		exec startx
	fi
}
# }}}

# Fzf {{{
if (( $+commands[fzf] )); then
	local FZF_KEYBINDS=/usr/share/fzf/key-bindings.zsh
	local FZF_COMPLETION=/usr/share/fzf/completion.zsh
	source $FZF_KEYBINDS
	source $FZF_COMPLETION

	# Stop fzf completion trigger from colliding with zsh glob operator
	export FZF_COMPLETION_TRIGGER="++"

	# Fzf options
	local fzf_options=(
		--height=30%
		--layout=reverse
		--info=inline
		--multi
		--bind "?:toggle-preview,ctrl-a:toggle-all"
	)

	# Colorscheme overrides
	local colors=(
		--color="fg+:#ebdbb2"
	)

	fzf_options+=(${colors[@]})

	# Variables and functions for fzf operation
	export FZF_DEFAULT_OPTS=${fzf_options[@]}

	if (( $+commands[fd] )); then
		local FD_DEFAULT_OPTS="--hidden --exclude .git"
		export FZF_DEFAULT_COMMAND="fd --type f $FD_DEFAULT_OPTS"
		export FZF_CTRL_T_COMMAND="fd --type f $FD_DEFAULT_OPTS"
		export FZF_ALT_C_COMMAND="fd --type d $FD_DEFAULT_OPTS"

		_fzf_compgen_path() {
			fd --hidden --exclude ".git" . "$1"
		}

		_fzf_compgen_dir() {
			fd --type d --hidden --exclude ".git" . "$1"
		}
	fi
fi
# }}}

# Plugins {{{
local FORGIT_PLUGIN=/usr/share/zsh/plugins/forgit-git/forgit.plugin.zsh
[ -f $FORGIT_PLUGIN ] && source $FORGIT_PLUGIN

(( $+commands[zoxide] )) && eval "$(zoxide init zsh --cmd j)"
# }}}
